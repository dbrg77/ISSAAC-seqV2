configfile: 'config.json'

rule all:
    input:
        'fastq/RNA_CB_UMI.fq.gz',
        'star_outs/star.done',
        'chromap_outs/summary.csv',
        'chromap_outs/fragments.tsv.gz',
        'chromap_outs/fragments.tsv.gz.tbi',
        'chromap_outs/qc_num_frags_in_peaks.csv',
        'chromap_outs/qc_num_chrM.csv',
        'chromap_outs/tagAlign_no_chrM.bed.gz',
        'macs2_pk/aggregate_peaks.narrowPeak',
        'macs2_pk/aggregate_treat_pileup.bdg',
        'macs2_pk/aggregate_treat_pileup.bw',
        'macs2_pk/macs2.done',
        'macs2_pk/aggregate_peaks_formatted.bed',
        'chromap_outs/qc_info.csv',
        'chromap_outs/peak_read_ov.tsv.gz',
        'chromap_outs/raw/features.tsv',
        'chromap_outs/raw/barcodes.tsv',
        'chromap_outs/raw/matrix.mtx',
        'chromap_outs/filtered/features.tsv',
        'chromap_outs/filtered/barcodes.tsv',
        'chromap_outs/filtered/matrix.mtx'

rule prepareRNACBUMI:
    input:
        cb=config['rna_cb_fq'],
        umi=config['rna_umi_fq']
    output:
        'fastq/RNA_CB_UMI.fq.gz'
    shell:
        ''' paste <(zcat {input.cb}) <(zcat {input.umi}) | \
            awk -F "\t" '{{ if(NR%4==1||NR%4==3) {{print $1}} else {{print $1 substr($2,1,10)}} }}' | \
            gzip > {output}
        '''

rule alignRNA:
    input:
        cdna=config['rna_cdna_fq'],
        cbumi='fastq/RNA_CB_UMI.fq.gz'
    output:
        touch('star_outs/star.done')
    log:
        stdout='logs/star.stdout',
        stderr='logs/star.stderr'
    params:
        pg=config['star_bin'],
        idx=config['star_idx'],
        wl=config['whitelist'],
        ft=config['star_ft'],
        trim3=config['star_trim3'],
        mmNmax=config['mmNmax'],
        configbc=config['star_configbc'],
        cbumipos=config['star_cbumipos'],
        strand=config['star_strand'],
        samattr=config['star_samattr']
    threads: 40
    shell:
        ''' {params.pg} --runThreadN {threads} \
            --genomeDir {params.idx} \
            --readFilesCommand zcat \
            --outFileNamePrefix star_outs/ \
            --outFilterMultimapNmax {params.mmNmax} \
            --readFilesIn {input.cdna} {input.cbumi} \
            --clip3pNbases {params.trim3} \
            --soloFeatures {params.ft} \
            --soloType {params.configbc} {params.cbumipos} \
            --soloCBwhitelist {params.wl} \
            --soloCellReadStats Standard \
            --soloCellFilter EmptyDrops_CR \
            --soloStrand {params.strand} \
            --outSAMattributes {params.samattr} \
            --outSAMtype BAM SortedByCoordinate \
            1> {log.stdout} 2> {log.stderr} && \
            gzip star_outs/Solo.out/{params.ft}/raw/* \
                 star_outs/Solo.out/{params.ft}/filtered/*
        '''

rule alignATAC:
    input:
        r1=config['atac_r1_fq'],
        r2=config['atac_r2_fq'],
        cb=config['atac_cb_fq']
    output:
        frag='chromap_outs/fragments.tsv.gz',
        qc='chromap_outs/summary.csv'
    log:
        'logs/chromap.stderr'
    params:
        pg=config['chromap_bin'],
        idx=config['chromap_idx'],
        fa=config['chromap_fa'],
        wl=config['whitelist']
    threads: 40
    shell:
        ''' {params.pg} -t {threads} -x {params.idx} -r {params.fa} \
            --preset atac --summary {output.qc} \
            -1 {input.r1} -2 {input.r2} -b {input.cb} \
            --barcode-whitelist {params.wl} \
            -o /dev/stdout | bgzip > {output.frag} \
            2> {log}
        '''

rule indexFrag:
    input:
        'chromap_outs/fragments.tsv.gz'
    output:
        'chromap_outs/fragments.tsv.gz.tbi'
    shell:
        'tabix -p bed {input}'

rule chrMCheck:
    input:
        'chromap_outs/fragments.tsv.gz'
    output:
        'chromap_outs/qc_num_chrM.csv'
    shell:
        ''' zcat {input} | grep chrM | \
            cut -f 4 | sort | uniq -c | \
            awk 'BEGIN{{OFS=","}}{{print $2,$1}}' \
            > {output}
        '''

rule fragToTagAlign:
    input:
        frag='chromap_outs/fragments.tsv.gz',
        bl=config['blacklist'],
        csize=config['chrom_size']
    output:
        'chromap_outs/tagAlign_no_chrM.bed.gz'
    shell:
        ''' zcat {input.frag} | \
            sed '/chrM/d' | \
            awk 'BEGIN{{OFS="\t"}}{{ print $1, $2, $2+50, $4, ".", "+"; print $1, $3-50, $3, $4, ".", "-" }}' | \
            bedClip stdin {input.csize} stdout | \
            intersectBed -a - -b {input.bl} -v | \
            sort -k1,1 -k2,2n | \
            gzip > {output}
        '''

rule peakCalling:
    input:
        'chromap_outs/tagAlign_no_chrM.bed.gz'
    output:
        'macs2_pk/aggregate_treat_pileup.bdg',
        'macs2_pk/aggregate_peaks.narrowPeak',
        touch('macs2_pk/macs2.done')
    params:
        gs=config['gsize'],
        shift=config['macs2_shift']
    log:
        stdout='logs/macs2.stdout',
        stderr='logs/macs2.stderr'
    shell:
        ''' macs2 callpeak -t {input} -f BED -g {params.gs} \
            {params.shift} -B --SPMR --keep-dup all -q 0.01 \
            --outdir macs2_pk -n aggregate \
            1> {log.stdout} 2> {log.stderr}
        '''

rule formatPeak:
    input:
        'macs2_pk/aggregate_peaks.narrowPeak'
    output:
        'macs2_pk/aggregate_peaks_formatted.bed'
    shell:
        ''' cut -f 1-3 {input} | sort -k1,1 -k2,2n | \
            awk 'BEGIN{{OFS="\t"}}{{print $1, $2, $3, $1 "-" $2 "-" $3}}' > {output}
        '''

rule fragsInPeaks:
    input:
        frag='chromap_outs/fragments.tsv.gz',
        peak='macs2_pk/aggregate_peaks.narrowPeak'
    output:
        'chromap_outs/qc_num_frags_in_peaks.csv'
    shell:
        ''' intersectBed -a {input.frag} -b {input.peak} -u | \
            cut -f 4 | sort | uniq -c | \
            awk 'BEGIN{{OFS=","}}{{print $2,$1}}' \
            > {output}
        '''

rule bdgToBw:
    input:
        bdg='macs2_pk/aggregate_treat_pileup.bdg',
        cs=config['chrom_size']
    output:
        'macs2_pk/aggregate_treat_pileup.bw'
    shell:
        ''' bdg2bw {input.bdg} {input.cs}
        '''

rule collectQC:
    input:
        'chromap_outs/summary.csv',
        'chromap_outs/qc_num_frags_in_peaks.csv',
        'chromap_outs/qc_num_chrM.csv'
    output:
        'chromap_outs/qc_info.csv'
    script:
        'scripts/collect_qc.py'

rule peakReadOV:
    input:
        peaks='macs2_pk/aggregate_peaks_formatted.bed',
        reads='chromap_outs/tagAlign_no_chrM.bed.gz',
        cs=config['chrom_size']
    output:
        'chromap_outs/peak_read_ov.tsv.gz'
    shell:
        ''' intersectBed -a {input.peaks} -b {input.reads} \
            -wo -sorted -g {input.cs} | sort -k4,4 | \
            groupBy -g 4 -c 8 -o freqdesc | \
            gzip > {output}
        '''

rule prepareMTX1:
    input:
        pkov='chromap_outs/peak_read_ov.tsv.gz',
        frag='chromap_outs/fragments.tsv.gz'
    output:
        ft='chromap_outs/raw/features.tsv',
        bc='chromap_outs/raw/barcodes.tsv'
    shell:
        ''' zcat {input.pkov} | cut -f 1 > {output.ft}
            zcat {input.frag} | cut -f 4 | sort -u > {output.bc}
        '''

rule prepareMTX2:
    input:
        'chromap_outs/raw/features.tsv',
        'chromap_outs/raw/barcodes.tsv',
        'chromap_outs/peak_read_ov.tsv.gz'
    output:
        'chromap_outs/raw/matrix.mtx'
    script:
        'scripts/generate_csc_mtx.py'

rule cellCallingATAC:
    input:
        pg=config['star_bin'],
        ft='chromap_outs/raw/features.tsv',
        bc='chromap_outs/raw/barcodes.tsv',
        mt='chromap_outs/raw/matrix.mtx'
    output:
        'chromap_outs/filtered/features.tsv',
        'chromap_outs/filtered/barcodes.tsv',
        'chromap_outs/filtered/matrix.mtx'
    shell:
        ''' {input.pg} --runMode soloCellFiltering \
            chromap_outs/raw/ chromap_outs/filtered/ \
            --soloCellFilter EmptyDrops_CR
        '''
